buildscript {
	repositories {
		jcenter()
		maven {
			url "https://maven.google.com"
		}
		google()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:3.6.2'
	}
}

def properties = getProps()
def secrets = getSecrets()

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
	compileSdkVersion 29

	defaultConfig {
		applicationId "uk.co.markormesher.android_fab.app"

		minSdkVersion 21
		targetSdkVersion 29

		versionCode properties['version_code']
		versionName properties['version_name']
	}

	sourceSets {
		main.java.srcDirs += "src/main/kotlin"
	}

	signingConfigs {
		main {
			storeFile secrets.isEmpty() ? null : file(secrets['KEYSTORE_PATH'])
			storePassword secrets['KEYSTORE_PASSWORD']
			keyAlias secrets['KEYSTORE_KEY_ALIAS']
			keyPassword secrets['KEYSTORE_KEY_PASSWORD']
		}
	}

	buildTypes {
		release {
			// signing
			//noinspection GroovyAssignabilityCheck
			signingConfig signingConfigs.main

			// minify
			minifyEnabled true
			proguardFile 'proguard-rules.pro'

			// debug
			buildConfigField "boolean", "DEBUG_MODE", "false"
			debuggable false
		}

		debug {
			// minify
			minifyEnabled false

			// debug
			buildConfigField "boolean", "DEBUG_MODE", "true"
			debuggable true
		}
	}
}

repositories {
	jcenter()
	maven {
		url "https://maven.google.com"
	}
	google()
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'com.google.android.material:material:1.2.0-alpha05'
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
	implementation project(':fab')
}
